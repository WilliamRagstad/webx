// This is an example WebX todo app project.
{
    // Global in-memory database of todos for this example.
    const todos = [];
}

model Todo {
    title: String,
    completed: Boolean,
    createdAt: Date,
}

handler getTimeDiff(then: Date) -> String {
    const diff = new Date().getTime() - then.getTime();
    const seconds = Math.floor(diff / 1000);
    const minutes = Math.floor(seconds / 60);
    const hours = Math.floor(minutes / 60);
    const days = Math.floor(hours / 24);

    if (days > 0) { return `${days} days ago`; }
    else if (hours > 0) { return `${hours} hours ago`; }
    else if (minutes > 0) { return `${minutes} minutes ago`; }
    else if (seconds > 0) { return `${seconds} seconds ago`; }
    else { return "just now"; }
}

handler renderTodo(todo: Todo) -> HTML <h1>
    <input type="checkbox" checked={todo.completed} />
    {todo.title} - {getTimeDiff(todo.createdAt)}
</h1>

handler renderAllTodos(todos: Todo[]) -> HTML {
    return (<ul>{todos.map(renderTodo)}</ul>);
}

get /about <div>
    <h1>About</h1>
    <p>This is an example WebX project.</p>
</div>

location /todo {
    // Display the global list of todos as HTML.
    get /list -> renderAllTodos(todos)

    // Add a new todo to the list with the given title.
    // { title: "My Todo" }
    // returns HTML
    post /add json(title: String) {
        const newTodo = {
            title,
            completed: false,
            createdAt: new Date()
        };
        todos.push(newTodo);
    } -> renderTodo(newTodo)

    // Toggle the completed status of the todo with the given id.
    // { id: 0 }
    // returns HTML
    post /toggle json(id: Int) {
        const todo = todos.find(t => t.id === id);
        if (todo) {
            todo.completed = !todo.completed;
            return renderTodo(todo);
        } else {
            return error("Todo not found.");
        }
    }
}
